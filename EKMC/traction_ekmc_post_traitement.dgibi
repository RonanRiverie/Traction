******************************************
* RECUPERATION DES RESULTATS DES CALCULS *
******************************************

* RESTITUTION DES RESULTATS DES CALCULS
OPTI 'REST' 'traction_ekmc_calcul.sauv' ; 
REST ;

* RECUPERATION DES TEMPS, DEPLACEMENTS, CONTRAINTES ET REACTIONS A CHAQUE PAS DE TEMPS
TAB_TEMPS = TAB1 . 'TEMPS' ;
TAB_DEP = TAB1 . 'DEPLACEMENTS' ;
TAB_SIG = TAB1 . 'CONTRAINTES' ;
TAB_REAC = TAB1 . 'REACTIONS' ;

* NOMBRE DE PAS DE TEMPS UTILISES DANS PASAPAS
N_TEMPS = DIME TAB_TEMPS ;

****************************************
* CONTRAINTES - DEPLACEMENTS - EFFORTS *
****************************************

* DEFORMEE *

* TRACE DE LA DEFORMEE ET DE LA CONFIGURATION INITIALE
U_F = TAB_DEP . (N_TEMPS - 1) ;
DEF_T_F = DEFO CONTSU_EP U_F 1. 'ROUG' ;
DEF_T_0 = DEFO CONTSU_EP U_F 0. ;
* TRAC (DEF_T_0 ET DEF_T_F) ;

* OBTENTION DE LA COURBE CONTRAINTE - DEFORMATION *

* INITIALISATION DES LISTES DE DEFORMATION ET DE CONTRAINTE
LISTE_TEMPS = PROG ;
LISTE_EPS = PROG ;
LISTE_SIG = PROG ;
* BOUCLE SUR CHAQUE PAS DE TEMPS
REPE B1 N_TEMPS ;
*   INDICE DE LA TABLE PASAPAS AU PAS DE TEMPS CONSIDERE
    I_TEMPS = &B1 - 1 ;
*   INSTANT CONSIDERE
    TEMPS_I = (TAB1 . 'TEMPS') . I_TEMPS ;
*   MISE A JOUR DE LA LISTE DES INSTANTS CALCULES
    LISTE_TEMPS = LISTE_TEMPS ET TEMPS_I ;
*   RECUPERATION DES DEPLACEMENTS AU PAS DE TEMPS CONSIDERE
    U_I = TAB_DEP . I_TEMPS ;
*   DEPLACEMENT SELON Y AU NIVEAU DES POINTS DE L'EXTENSOMETRE
    U_Y_EXT_H = EXTR U_I 'UY' P_EXT_H ;
    U_Y_EXT_B = EXTR U_I 'UY' P_EXT_B ;
    DELTA_L_EXT = U_Y_EXT_H - U_Y_EXT_B ;
*   DEFORMATION ASSOCIEE A L'EXTENSOMETRE
    EPS_EXT = DELTA_L_EXT / L0_EXT ;
    LISTE_EPS = LISTE_EPS ET EPS_EXT ;
*   RECUPERATION DES REACTIONS AU PAS DE TEMPS CONSIDERE
    REAC_I = TAB_REAC . I_TEMPS ;
*   FORCE RESULTANTE DES EFFORTS AU NIVEAU DE LA LIGNE D'APPLICATION DE LA FORCE
    FORCE_I = MAXI 'ABS' (RESU (REDU REAC_I LI_H)) ;
*   CALCUL DE LA CONTRAINTE A PARTIR DE LA FORCE
    SIG_I = FORCE_I / PHI ;
    LISTE_SIG = LISTE_SIG ET SIG_I ;
FIN B1 ;

* EXPORT EN CSV DE LA DEFORMATION EN FONCTION DU TEMPS
EVO_TEMPS_EPS = EVOL 'MANU' LISTE_TEMPS 'Temps' LISTE_EPS 'Deformation' ;
OPTI 'SORT' 'Temps_Deformation';
SORT 'EXCE' EVO_TEMPS_EPS ;

* EXPORT EN CSV DE LA CONTRAINTE EN FONCTION DU TEMPS
EVO_TEMPS_SIG = EVOL 'MANU' LISTE_TEMPS 'Temps' LISTE_SIG 'Contrainte' ;
OPTI 'SORT' 'Temps_Contrainte';
SORT 'EXCE' EVO_TEMPS_SIG ;

FIN ;