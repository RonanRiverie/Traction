* OPTIONS GENERALES ET TYPE D'ELEMENTS GEOMETRIQUES
OPTI 'DIME' 2 'ELEM' 'QUA8' ;

* DIMENSIONS DE L'EPROUVETTE EN [MM]
L_TOT = 82.E-3 ;
L2_TOT = 0.5 * L_TOT ;
L0 = 36.E-3 ;
L02 = 0.5 * L0 ;
PHI = 6.E-3 ;
R = 0.5 * PHI ;
SU0 = PI * (R**2.) ;
L_TETE = 15.E-3 ;
D_TETE = 14.E-3 ;
R_TETE = 0.5 * D_TETE ;
R_CONGE = 10.E-3 ;

* DONNES DE L'EXTENSOMETRE
L0_EXT = 24.E-3 ;
L02_EXT = 0.5 * L0_EXT ;

* MAILLAGE DU RECTANGLE DE LA TETE DROITE EN BAS DE L'EPROUVETTE *

* DEFINITION DES POINTS AUX SOMMETS DU RECTANGLES
PO = 0. 0. ;
PA = PO PLUS (R_TETE 0.) ;
PB = PO PLUS (R_TETE L_TETE) ;
PC = PO PLUS (0. L_TETE) ;
* CREATION DES LIGNES SUPPORTS DU RECTANGLE
LIAB = DROI 3 PA PB ;
LIOC = DROI 3 PO PC ;
* CREATION DU RECTANGLE EN SURFACE REGLEE
SU_TETE_D_BAS = REGL 3 LIAB LIOC ;
* INITIALISATION DU MAILLAGE DE L'EPROUVETTE
SU_EP_D_BAS = SU_TETE_D_BAS ;
* TRAC SU_EP_D_BAS ;

* MAILLAGE DE LA SURFACE DU CONGE EN BAS A DROITE DE L'EPROUVETTE *

* DEFINITION DES POINTS
LCE = L2_TOT - L02 - L_TETE ;
PE = PC PLUS (0. LCE) ;
PD = PE PLUS (R 0.) ;
P_CONGE_D_BAS_CE = PD PLUS (R_CONGE 0.) ;
* CREATION DU CERCLE DU CONGE
CE_CONGE_D_BAS = CERC 6 PB P_CONGE_D_BAS_CE PD ;
* DEFINITION DE LA LIGNE SUR L'AXE DE L'EPROUVETTE
LICE = DROI 6 PC PE ;
* MAILLAGE EN SURFACE REGLEE DU CONGE
SU_CONGE_D_BAS = REGL 3 CE_CONGE_D_BAS LICE ;
* MISE A JOUR DU MAILLAGE DE L'EPROUVETTE
SU_EP_D_BAS = SU_EP_D_BAS ET SU_CONGE_D_BAS ;
* TRAC SU_EP_D_BAS ;

* MAILLAGE DE LA PARTIE DROITE DE LA SECTION UTILE BASSE *

* DEFINITION DES POINTS
PF = PE PLUS (0. L02) ;
PG = PF PLUS (R 0.) ;
* DEFINITION DES LIGNES SUPPORTS DU RECTANGLE
L_EF_L0 = 1.E-3 ;
N_L0 = ENTI 'SUPE' (L02 / L_EF_L0) ;
LIDG = DROI N_L0 PD PG ;
LIEF = DROI N_L0 PE PF ;
* MAILLAGE DU RECTANGLE EN SURFACE REGLEE
SU_D_L0 = REGL 3 LIDG LIEF ;
* MISE A JOUR DU MAILLAGE DE L'EPROUVETTE
SU_EP_D_BAS = SU_EP_D_BAS ET SU_D_L0 ;
* TRAC SU_EP_D_BAS ;

* MAILLAGE DE LA PARTIE DROITE DE L'EPROUVETTE *

* SYMETRIE DU BAS DE L'EPROUVETTE POUR CREER LE MAILLAGE EN HAUT
SU_EP_D_HAUT = SU_EP_D_BAS SYME 'DROI' PF PG ;

* MAILLAGE DE LA PARTIE DROITE DE L'EPROUVETTE PAR ASSEMBLAGE DES MAILLAGES HAUT ET BAS
SU_EP_D = SU_EP_D_BAS ET SU_EP_D_HAUT ;
* TRAC SU_EP_D ;

* MAILLAGE DE L'EPROUVETTE DANS SON ENSEMBLE *

* SYMETRIE DE LA PARTIE DROITE DE L'EPROUVETTE POUR CREER LA PARTIE GAUCHE
SU_EP_G = SU_EP_D SYME 'DROI' PO PC ;

* MAILLAGE GLOBAL DE L'EPROUVETTE PAR ASSEMBLAGE DES MAILLAGES GAUCHE ET DROITE
SU_EP = SU_EP_D ET SU_EP_G ;
ELIM SU_EP 1.E-9 ;
CONTSU_EP = CONT SU_EP ;
* TRAC SU_EP ;

* POSITIONNEMENT THEORIQUE DES POINTS DE L'EXTENSOMETRE
P_EXT_H_TH = PG PLUS ((-1. * PHI) L02_EXT) ;
P_EXT_B_TH = P_EXT_H_TH MOIN (0. L0_EXT) ;
* POSITIONNEMENT SUR LE MAILLAGE DES POINTS DE L'EXTENSOMETRE
P_EXT_H = SU_EP POIN 'PROC' P_EXT_H_TH ;
P_EXT_B = SU_EP POIN 'PROC' P_EXT_B_TH ;
* TRAC (SU_EP ET P_EXT_H ET P_EXT_B) ;

* SAUVEGARDE DU MAILLAGE *

OPTI 'SAUV' 'traction_maillage.sauv' ; 
SAUV ; 

FIN ;