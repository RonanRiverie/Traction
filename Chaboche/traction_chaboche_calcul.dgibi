*********************************************************
* RESTITUTION DES OBJETS DU MAILLAGE ET DE LA GEOMETRIE *
*********************************************************

OPTI 'REST' 'traction_maillage.sauv' ; 
REST ;

*************************************
* DEFINITION DES MODELES MECANIQUES *
************************************* 

* HYPOTHESE DES CONTRAINTES PLANES *
OPTI 'MODE' 'PLAN' 'CONT' ;

* DEFINITION DU MODELE MECANIQUE *

 coel = 'MOTS' 'YOUN' 'NU' 'RHO' 'ALPH' 'DIM3' 'TALP' 'TREF' 'XYOU'
'XNU' 'XR0' 'XQ' 'XB';

 statev = 'MOTS' 'EEXX' 'EEYY' 'EEZZ' 'EEXY' 'P' 'ETOZ';

 MO_EP = 'MODE' SU_EP 'MECANIQUE' 'ELASTIQUE' 'NON_LINEAIRE'
'UTILISATEUR' 'LIB_LOI' 'src\libUmatBehaviour.dll' 'FCT_LOI' 'umatchaboche'
'C_MATERIAU' coel 'C_VARINTER' statev ;

* DEFINITION DU MATERIAU *

* CARACTERISTIQUES MATERIAU
E_EP = 209.E9 ;
NU_EP = 0.3 ;
R0_EP = 260.E6 ;
Q_EP = 222.3E6 ;
B_EP = 20. ;

* MODELE DU MATERIAU
 MA_EP = 'MATE' MO_EP 'YOUN' E_EP 'NU' NU_EP 'RHO' 0. 'ALPH' 0. 'DIM3' 1.
'TALP' 0. 'TREF' 0. 'XYOU' E_EP 'XNU' NU_EP 'XR0' R0_EP 'XQ' Q_EP
'XB' B_EP ;

**********************
* CONDITIONS LIMITES *
**********************

* RECUPERATION DES POINTS DU MAILLAGE
X_SU_EP = COOR 1 SU_EP ;
Y_SU_EP = COOR 2 SU_EP ;

* CONDITION LIMITE SUR LA LIGNE SUPERIEURE D'APPLICATION DE L'EFFORT *

* RECUPERATION DES POINTS DE LA LIGNE DU HAUT
P_LI_H = Y_SU_EP POIN 'MAXI' ;
* RECUPERATION DE LA LIGNE CORRESPONDANTE
LI_H = CONTSU_EP ELEM 'APPU' P_LI_H ;
* TRAC (SU_EP ET (LI_H 'COUL' 'ROUG')) ;
* BLOCAGE DES DEPLACEMENTS SELON UY 
CL_LI_H = BLOQ 'UY' LI_H ;

* CONDITION LIMITE SUR LA LIGNE INFERIEURE *

* RECUPERATION DES POINTS DE LA LIGNE DU BAS
P_LI_B = Y_SU_EP POIN 'MINI' ;
* RECUPERATION DE LA LIGNE CORRESPONDANTE
LI_B = CONTSU_EP ELEM 'APPU' P_LI_B ;
* TRAC (SU_EP ET (LI_B 'COUL' 'ROUG')) ;
* BLOCAGE DES DEPLACEMENTS SELON UY 
CL_LI_B = BLOQ 'UY' LI_B ;

* BLOCAGE DU POINT MILIEU SUR LA LIGNE DU BAS *

* BLOCAGE DU DEPLACEMENT SELON UX
CL_PO = BLOQ 'UX' PO ;

****************************
* DEFINITION DU CHARGEMENT *
****************************

* DEPLACEMENT IMPOSE AU NIVEAU DE LA LIGNE D'APPLICATION DE L'EFFORT
EPS_VAL = 0.02 ;
DEP_VAL = EPS_VAL * L0 ;
DEP_FORC = DEPI CL_LI_H DEP_VAL ;

* EVOLUTION DU CHARGEMENT DANS LE TEMPS
T_0 = 0. ;
EPS_POINT = 1. ;
T_F = EPS_VAL / EPS_POINT ;
EPS_R0 = 0.0025 ;
T_R_0 = EPS_R0 / EPS_POINT ;
LISTE_TEMPS_TEMPS = PROG T_0 T_R_0 T_F ;
LISTE_TEMPS_CHAR = PROG 0. (EPS_R0 / EPS_VAL) 1. ;
EVO_DEP_FORC = EVOL 'MANU' LISTE_TEMPS_TEMPS LISTE_TEMPS_CHAR ;
CHAR_DEP_FORC = CHAR 'DIMP' DEP_FORC EVO_DEP_FORC ;

***************************************
* RESOLUTION DU PROBLEME AVEC PASAPAS *
***************************************

* LISTE DES TEMPS CALCULES
N_R0 = 20 ;
D_T_R_0 = T_R_0 / N_R0 ;
N_T = 20 ;
D_T = ((T_F - T_R_0) / N_T) ;
LISTE_CALC_TEMPS = PROG T_0 'PAS' D_T_R_0 T_R_0 'PAS' D_T T_F ;

* DEFINITION DE LA TABLE 
TAB1 = TABLE ;
TAB1 . 'MODELE' = MO_EP ;
TAB1 . 'CARACTERISTIQUES' = MA_EP ;
TAB1 . 'BLOCAGES_MECANIQUES' = CL_LI_H ET CL_LI_B ET CL_PO ;
TAB1 . 'CHARGEMENT' = CHAR_DEP_FORC ;
TAB1 . 'TEMPS_CALCULES' = LISTE_CALC_TEMPS ;

* LANCEMENT DE PASAPAS
PASAPAS TAB1 ;

**************************
* SAUVEGARDE DES CALCULS *
**************************

OPTI 'SAUV' 'traction_chaboche_calcul.sauv' ; 
SAUV; 

FIN ;